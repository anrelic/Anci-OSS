buildscript {
    apply from: "dependencies.gradle"
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "http://dl.bintray.com/jamefrus/maven"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
                'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7',
                lib.junit["plugin"],
                "su.jfdev.gradle.struct:gradle-struct:1.4.5"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'su.jfdev.struct'
    apply plugin: 'com.jfrog.bintray'

    group 'su.jfdev.anci'
    version '0.1.2'

    repositories {
        jcenter()
        maven {
            url  "http://dl.bintray.com/anci/maven"
        }
    }

    dependencies {
        apiCompile lib.kotlin["runtime"]
        compile lib.kotlin["std"]

        testCompile lib.jupiter["api"], lib.junit["platform"]
        testRuntime lib.jupiter["engine"]
    }

    junitPlatform {
        engines {
            include 'junit-jupiter'
        }
    }

    ext.fullname = "anci-$name"
    describe {
        api.publish "$fullname-api"
        main.publish "$fullname-main"
    }

    bintray {
        user = property("binuser")
        key = property("binkey")
        publish = true
        publications = described
        pkg {
            userOrg = "anci"
            repo = 'maven'
            name = fullname
            licenses = ['MIT']
            vcsUrl = 'https://github.com/Jamefrus/OpenAnci.git'
            version {
                released = new Date()
            }
        }
    }

}
