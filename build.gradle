buildscript {
    apply from: "dependencies.gradle"
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "http://dl.bintray.com/jamefrus/maven"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
                "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}",
                'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7',
                lib.junit["plugin"],
                "su.jfdev.gradle.struct:gradle-struct:1.5.1"
    }
}

subprojects {
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'su.jfdev.struct'
    apply plugin: 'com.jfrog.bintray'

    group 'su.jfdev.anci'
    version '0.1.0'

    repositories {
        jcenter()
        maven {
            url  "http://dl.bintray.com/anci/maven"
        }
    }

    dependencies {
        apiCompile lib.kotlin["runtime"]
        compile lib.kotlin["std"]

        testCompile lib.jupiter["api"], lib.junit["platform"]
        testRuntime lib.jupiter["engine"]
    }

    junitPlatform {
        engines {
            include 'junit-jupiter'
        }
    }

    describe {
        api
        main
    }

    publish {
        api
        main
    }

    ext.fullname = "anci-$name"
    bintray {
        user = property("binuser")
        key = property("binkey")
        publish = true
        publications = described
        pkg {
            userOrg = "anci"
            repo = 'maven'
            name = fullname
            licenses = ['MIT']

            vcsUrl = 'https://github.com/anrelic/Anci-OSS.git'
            websiteUrl = 'https://github.com/anrelic/Anci-OSS'
            issueTrackerUrl = 'https://github.com/anrelic/Anci-OSS/issues'

            githubRepo = 'anrelic/Anci-OSS'

            version {
                released = new Date()
            }
        }
    }

}
