buildscript {
    apply from: "dependencies.gradle"
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "http://dl.bintray.com/jamefrus/maven"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
                lib.junit["plugin"],
                "su.jfdev.gradle.struct:gradle-struct:1.4.4"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

subprojects {
    apply plugin: 'su.jfdev.struct'
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'maven-publish'

    group 'su.jfdev.anci'
    version '0.1.0'

    repositories {
        jcenter()
    }

    dependencies {
        apiCompile lib.kotlin["runtime"]
        compile lib.kotlin["std"]

        testCompile lib.jupiter["api"], lib.junit["platform"]
        testRuntime lib.jupiter["engine"]
    }

    junitPlatform {
        engines {
            include 'junit-jupiter'
        }
    }

    publishing {
        publications {
            general(MavenPublication){
                artifactId "anci-$name"
            }
        }
    }

    bintray {
        user = property("binuser")
        key = property("binkey")
        configurations = ['general']
        pkg {
            repo = 'temp'
            name = "anci-$name"
            userOrg = property("binuser")
            licenses = ['MIT']
            vcsUrl = 'https://github.com/Jamefrus/OpenAnci.git'
            version {
                name = project.version
                released = new Date()
                vcsTag = project.version
            }
        }
    }
}